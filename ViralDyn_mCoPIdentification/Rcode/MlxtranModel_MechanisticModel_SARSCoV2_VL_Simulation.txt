; Mlxtran model used in R (mlxR package) to simulated viral load with the mechanistic ODE-based model
; with immuno markers considered as external data
; Model with Nasopharynx and Trachea compartments jointly considered
; Model with four outputs: 
;   - viral load (gRNA) in trachea
;   - viral load (gRNA) in nasopharynx
;   - subgenomic viral load (sgRNA) in trachea
;   - subgenomic viral loas (sgRNA) in nasopharynx
; Model with random effects on two parameters: the viral infectivity (beta) and the loss of infected cells (delta)


[LONGITUDINAL]
input = {beta_N_pow,fact_beta_T,fact_delta_T,delta_N,c,cI,k,g,mu,P_N,fact_P_T,alpha_VLSG,Weight,thresh_Weight,phi_immuno,Marker_slope,Marker_intercept,a1,a2,a3,a4}

Weight = {use=regressor}
Marker_slope = {use=regressor}
Marker_intercept = {use=regressor}


EQUATION:
odeType = stiff

; Physiological quantities
; ------------------------
if(Weight <= thresh_Weight)
	W_N = 4
	W_T = 2
else
	W_N = 5.5
	W_T = 3
end

InoculumBaseline=4.37E9*5    ; Total amount of Inoculum injected in copies

 
; Initial Conditions
; ------------------
t0=0
ViniT_0 = 0.9*InoculumBaseline/W_T   ; in cells/ml
ViniN_0 = 0.1*InoculumBaseline/W_N	 ; in cells/ml
VIT_0 = 0  
VIN_0 = 0
VNIT_0 = 0
VNIN_0 = 0

TN_0 = 1.25*10^5/W_N ; 	From Gonçalves et al. (2020) ; in cells/ml
TT_0 = 2.25*10^4/W_T ; 	From Gonçalves et al. (2020) ; in cells/ml
I1T_0 = 0;
I1N_0 = 0;
I2T_0 = 0;
I2N_0 = 0;

; Linear interpolation of immuno marker
Marker = Marker_slope*t + Marker_intercept 


; Dynamical model
; ---------------
beta_N = 10^(beta_N_pow)
beta_N_COV = beta_N*exp(phi_immuno*Marker)
beta_T_COV = beta_N_COV*exp(fact_beta_T)

delta_T = delta_N*exp(fact_delta_T)		

P_T = P_N*exp(fact_P_T)

; Model for Trachea (concentration)
ddt_TT    = -beta_T_COV*TT*VIT - mu*beta_T_COV*TT*ViniT ; 
ddt_I1T   = beta_T_COV*TT*VIT + mu*beta_T_COV*TT*ViniT - k*I1T ; 
ddt_I2T   = k*I1T - delta_T*I2T ; 
ddt_VIT   = P_T*I2T*mu - c*VIT -beta_T_COV*TT*VIT -g*VIT +g*VIN ; 
ddt_VNIT  = P_T*I2T*(1-mu) - c*VNIT -g*VNIT +g*VNIN ;
ddt_ViniT = -cI*ViniT - mu*beta_T_COV*TT*ViniT ; 			Equation of the inoculum

; Model for Nasopharynx (concentration)
ddt_TN    = -beta_N_COV*TN*VIN - mu*beta_N_COV*TN*ViniN; 
ddt_I1N   = beta_N_COV*TN*VIN + mu*beta_N_COV*TN*ViniN - k*I1N ; 
ddt_I2N   = k*I1N - delta_N*I2N ; 
ddt_VIN   = P_N*I2N*mu - c*VIN -beta_N_COV*TN*VIN +g*VIT -g*VIN ;
ddt_VNIN  = P_N*I2N*(1-mu) - c*VNIN +g*VNIT -g*VNIN ; 
ddt_ViniN = -cI*ViniN - mu*beta_N_COV*TN*ViniN ;			Equation of the inoculum


; We impose positive values to I1 and I2
I1N_pos = max(I1N,0)
I2N_pos = max(I2N,0)
I1T_pos = max(I1T,0)
I2T_pos= max(I2T,0)

; Observation Model 
if t == 0
  f_VLSGTrachea = -5  
  f_VLTrachea   = -5
  f_VLSGNaso    = -5
  f_VLNaso      = -5
else 
  f_VLSGTrachea = alpha_VLSG*max(log10((I1T_pos+I2T_pos)),-5)    
  f_VLTrachea = max(log10((VIT+VNIT+ViniT)),-5)   
  f_VLSGNaso = alpha_VLSG*max(log10((I1N_pos+I2N_pos)),-5)  
  f_VLNaso = max(log10((VIN+VNIN+ViniN)),-5) 
end

; Error model
DEFINITION:
VLTrachea = {distribution=normal,prediction=f_VLTrachea,sd=a1}
VLSGTrachea = {distribution=normal,prediction=f_VLSGTrachea,sd=a2}
VLNaso = {distribution=normal,prediction=f_VLNaso,sd=a3}
VLSGNaso = {distribution=normal,prediction=f_VLSGNaso,sd=a4}
; -------------------------------------

; Addition of inter-individual variability on the parameters beta and delta

[INDIVIDUAL]
input={beta_N_pow_pop,delta_N_pop,omega_beta_N_pow,omega_delta_N}

DEFINITION:
beta_N_pow = {distribution = normal, prediction = beta_N_pow_pop, sd = omega_beta_N_pow}
delta_N = {distribution = lognormal, prediction = delta_N_pop, sd = omega_delta_N}